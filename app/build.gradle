plugins {
    alias(libs.plugins.androidApplication)
    alias(libs.plugins.jetbrainsKotlinAndroid)
}

android {
    namespace 'io.agora.board.forge.ui.sample'
    compileSdk 36

    defaultConfig {
        applicationId "io.agora.board.forge.ui.sample"
        minSdk 21
        targetSdk 35
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a'
        }

        buildConfigField "String", "BUILD_VERSION", "\"${getBuildVersion()}\""
    }

    buildFeatures {
        viewBinding true
        buildConfig = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def version = getBuildVersion()
            outputFileName = "forge-ui-demo-${version}.apk"
        }
    }
}

dependencies {

    implementation libs.androidx.core.ktx
    implementation libs.androidx.appcompat
    implementation libs.material
    implementation libs.androidx.activity
    implementation libs.androidx.constraintlayout
    implementation libs.gson
    testImplementation libs.junit
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.espresso.core

    implementation libs.agora.rtm
    implementation libs.gson

    implementation libs.bugly.crashreport
    implementation libs.okhttp3.okhttp

    if (project.findProject(':forge-ui') != null) {
        implementation project(':forge-ui')
    } else {
        implementation libs.agora.board.forge.ui
    }
    if (project.findProject(':forge-room') != null) {
        implementation project(':forge-room')
    } else {
        implementation libs.agora.board.forge.room
    }
    implementation libs.agora.board.forge.rtmprovider
}

import java.text.SimpleDateFormat

def getBuildVersion() {
    def formatter = new SimpleDateFormat("MMdd.HHmm")
    return formatter.format(new Date())
}
